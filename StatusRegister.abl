Module      StatusRegister
INTERFACE  (MuxOut7..MuxOut0,
            CarryOutMSB, CarryInMSB,
            ShiftRotateCarryOut,

            Subtract,
            ArithmeticOperation, FunctionOperation, ShiftRotateOperation,
            Clock, Reset, Hold
            -> SignFlag, OverflowFlag, CarryFlag, ZeroFlag);

TITLE       'StatusRegister for Flag Updates'


" Description:  This module computes and updates the ALU status flags given
"               the accumulator output, adder block output, and shift block output.
"               The SignFlag and ZeroFlag are affected by every operation, so they are
"               always computed. On the other hand, CarryFlag and OverflowFlag are affected
"               only by Arithmetic operations or Shift/Rotate operations, so the equations
"               reflect this (via a mux).
" Inputs:       MuxOut[7..0]             - 8-bit mux output from accumulator block
"               CarryOutMSB              - carry out of the MSB from the adder
"               CarryInMSB               - carry into the MSB from the adder
"               ShiftRotateCarryOut      - bit shifted out during shift/rotate
"
"               Subtract                 - active high when operation is subtraction-type
"               ArithmeticOperation      - active high when current op is arithmeti
"               FunctionOperation        - active high when current op is logical/function
"               ShiftRotateOperation     - active high when current op is shift/rotate
"               Clock                    - system clock
"               Reset                    - system reset
"               Hold                     - active high when we should hold
"
" Outputs:      SignFlag                 - sign flag
"               OverflowFlag             - overflow flag for signed arithmetic
"               CarryFlag                - carry/borrow flag or shift carry
"               ZeroFlag                 - zero flag


" Revision History:
" 02/26/26   Wilson Duan  Initial revision
" 02/27/26   Wilson Duan  Pass MuxOut into status register instead of Accum



" Pin/Signal Declarations

" Inputs
MuxOut7..MuxOut0        pin;  "input  8-bit mux output from accumulator block
CarryOutMSB             pin;    "input  carry out of MSB from adder
CarryInMSB              pin;    "input  carry into MSB from adder
ShiftRotateCarryOut     pin;    "input  bit shifted out during shift/rotate

Subtract                pin;    "input  if op is subtraction-type (CMP/DEC/SBB/SUB)
ArithmeticOperation     pin;    "input  if op is arithmetic
FunctionOperation       pin;    "input  if op is logic/function
ShiftRotateOperation    pin;    "input  if op is shift/rotate

Reset	pin;	"input  system reset
Clock   pin;	"input  system clock
Hold    pin;    "input  if we should hold

" Outputs
SignFlag                pin ISTYPE 'REG';    "output  sign flag
OverflowFlag            pin ISTYPE 'REG';    "output  overflow flag for signed arithmetic
CarryFlag               pin ISTYPE 'REG';    "output  carry/borrow or shift carry
ZeroFlag                pin ISTYPE 'REG';    "output  zero flag


EQUATIONS

" set clocks and reset
SignFlag.CLK = Clock;
SignFlag.CLR = !Reset;
OverflowFlag.CLK = Clock;
OverflowFlag.CLR = !Reset;
CarryFlag.CLK = Clock;
CarryFlag.CLR = !Reset;
ZeroFlag.CLK = Clock;
ZeroFlag.CLR = !Reset;

" compute sign and zero flags
" these are affected by all operations, so no need to enable
SignFlag := MuxOut7;
ZeroFlag := !(MuxOut7 # MuxOut6 # MuxOut5 # MuxOut4 #
              MuxOut3 # MuxOut2 # MuxOut1 # MuxOut0);

" compute carry and overflow flags
" these are modified by arithmetic or shifts, but not functional operations
CarryFlag := ((CarryOutMSB $ Subtract) & ArithmeticOperation) #
             (ShiftRotateCarryOut & ShiftRotateOperation) #
             (CarryFlag & FunctionOperation);

" ShiftRotateOverflow = LogicalShiftLeft & (MuxOut7 $ ShiftRotateCarryOut);
" ask TA about overflow for left shifts
OverflowFlag := ((CarryOutMSB $ CarryInMSB) & ArithmeticOperation) #
                (0 & ShiftRotateOperation) #
                (OverflowFlag & FunctionOperation);

End         StatusRegister